_ENV = Dora

commands =

	preload: (...) ->
		emit "Command.Preload", [...,]

	BGM: (filename) ->
		Audio\playStream filename, true, 0.5

	stopBGM: ->
		Audio\stopStream 1.0

	SE: (filename) ->
		Audio\play filename, false

	background: (filename, blur = true) ->
		emit "Command.Background", filename, blur

	noBackground: ->
		emit "Command.Background"

	figure: (filename, x = 0, y = 0, scale = 1.0) ->
		emit "Command.Figure", filename, x, y, scale

	noFigure: -> emit "Command.Figure"

	frame: (folder, duration, x = 0, y = 0, scale = 1.0, loop = true) ->
		emit "Command.Frame"
		emit "Command.Frame", folder, duration, loop, x, y, scale

	noFrame: -> emit "Command.Frame"

	inputName: ->
		import "utf-8" as u8
		import "UI.InputBox"
		import "Data.Config"
		with InputBox hint: "请输入你的姓名"
			.visible = false
			\schedule once ->
				sleep!
				.visible = true
			\addTo Director.ui
			\slot "Inputed", (name) ->
				Config.charName = u8.sub name, 1, 10
				\removeFromParent!
				emit "Story.Advance"
			coroutine.yield "Command"

	chapter: (filename) ->
		import "UI.LsdOSBack"
		import "UI.Story"
		import "Data.Config"
		if filename
			Config.chapter = filename
		else
			filename = Config.chapter
		Director.entry\removeAllChildren!
		Director.ui\removeAllChildren!
		Director.entry\gslot "Command.Background", (filename, blur) ->
			if child := Director.entry\getChildByTag "background"
				child\removeFromParent!
			if child := Director.ui\getChildByTag "background"
				child\removeFromParent!
			return unless filename
			bg = with? Sprite filename
				bgW, bgH = .width, .height
				updateBGSize = ->
					:width, :height = blur and View.size or App.bufferSize
					.width, .height = if bgW / bgH > width / height
						bgW * height / bgH, height
					else
						width, bgH * width / bgW
				updateBGSize!
				\gslot "AppChange", (settingName) -> if settingName == "Size"
					updateBGSize!
			return unless bg
			if blur
				Director.entry\addChild bg, -1, "background"
			else
				Director.ui\addChild bg, -1, "background"
		Director.entry\addChild with LsdOSBack!
			.order = -1
			\alignLayout!
		Director.ui\addChild with Story filename
			.order = 0
			\alignLayout!
			thread -> \showAsync!

	<index>: (_self, name) -> (...) ->
		print "[command]: #{name}(#{table.concat [tostring select i, ... for i = 1, select '#', ...], ', '})"

export default commands
